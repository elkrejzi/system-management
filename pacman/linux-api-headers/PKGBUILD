pkgname=linux-api-headers
_kver=4.1
pkgver=4.1.3
#pkgver=${_kver}
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace"
arch=('x86_64')
source=("https://www.kernel.org/pub/linux/kernel/v4.x/linux-${_kver}.tar.xz"
        "https://www.kernel.org/pub/linux/kernel/v4.x/patch-${pkgver}.xz")
md5sums=('fe9dc0f6729f36400ea81aa41d614c37'
         '6a388f552b05243ae4165735da839e24')

build() {
  cd "${srcdir}/linux-${_kver}"

  if [ -e "${srcdir}/patch-${pkgver}" ]
  then
    patch -Np1 -i "${srcdir}/patch-${pkgver}"
  fi

  make mrproper
}

package() {
  cd "${srcdir}/linux-${_kver}"

  make INSTALL_HDR_PATH="${pkgdir}/usr" headers_install

  rm -rf "${pkgdir}/usr/include/drm"

  find "${pkgdir}" \( -name .install -o -name ..install.cmd \) -delete
}
